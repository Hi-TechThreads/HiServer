plugins {
    id 'application'
    id 'org.springframework.boot' version '3.4.3'
    id 'com.google.protobuf'    version '0.9.4'
}

ext {
    springCloudVersion = "2024.0.1"
    grpcVersion = '1.62.2'
    grpcSpringBootStarterVersion = '3.0.0.RELEASE'
    protobufVersion = '3.25.1'
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
    }
}

// Protobuf Plugin Configuration
protobuf {
    // Configure the protoc executable
    protoc {
        artifact = "com.google.protobuf:protoc:${protobufVersion}"
    }
    // Configure the gRPC plugin
    plugins {
        grpc {
            artifact = "io.grpc:protoc-gen-grpc-java:${grpcVersion}"
        }
    }
    // Specify tasks to generate code and apply the gRPC plugin
    generateProtoTasks {
        all().each { task ->
            task.plugins {
                grpc {}
            }
            // Optional: Configure Java output options (e.g., if using lite runtime)
            // task.builtins { java { option 'lite' } }

            // The plugin typically configures the output directories correctly
            // and adds them to the source sets automatically.
            // Default location: build/generated/source/proto/main/grpc and build/generated/source/proto/main/java
        }
    }
    // Optional: Change where generated files go if needed
    // generatedFilesBaseDir = "$projectDir/src/generated/grpc"
}


dependencies {
    implementation project(':common')
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-security:'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-data-redis'
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'io.swagger.core.v3:swagger-annotations:2.0.5'
    implementation 'org.mapstruct:mapstruct:1.5.3.Final'

    // === gRPC Dependencies ===
    implementation "net.devh:grpc-spring-boot-starter:${grpcSpringBootStarterVersion}"
    implementation "io.grpc:grpc-protobuf:${grpcVersion}"
    implementation "io.grpc:grpc-stub:${grpcVersion}"
    implementation 'javax.annotation:javax.annotation-api:1.3.2'


    implementation 'org.springframework.cloud:spring-cloud-starter-netflix-eureka-client'
    compileOnly 'org.projectlombok:lombok:1.18.30'
    annotationProcessor 'org.projectlombok:lombok:1.18.30'
    annotationProcessor 'org.mapstruct:mapstruct-processor:1.5.3.Final'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'

    runtimeOnly 'com.h2database:h2'
    testImplementation 'com.h2database:h2'
}

application {
    mainClass = 'user.UserServerApplication'
}


test {
    useJUnitPlatform()
}